buildscript {
	ext {
		kotlinVersion = '1.3.10'
		springBootVersion = '2.0.5.RELEASE'
        dokkaVersion = '0.9.17'
	}
	repositories {
		mavenCentral()
		maven { url 'https://maven.google.com' }
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
		classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
	}
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-jpa'
apply plugin: 'kotlin-spring'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

group = 'com.radiotelescope'

version = '2.7.4-prod'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

dependencyManagement {
	imports {
		mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.330'
	}
}

processResources {
	def profile = (project.hasProperty('profile') ? project.profile : 'prod').toLowerCase()

	copy {
		from "src/main/resources/properties/application_${profile}.properties"
		into 'build/resources/main/'
		rename {
			'application.properties'
		}
	}
	copy {
		from 'src/main/resources/sql/schema-h2.sql'
		into 'build/resources'
	}
	copy {
		from 'src/main/resources/sql/seedTelescope.sql'
		into 'build/resources'
	}

	exclude("**/properties")
}

repositories {
	mavenCentral()
	mavenLocal()
}

dokka {
	outputFormat = 'javadoc'
	outputDirectory = "./docs"
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				includes = ['com.radiotelescope.contract.*',
							'com.radiotelescope.repository.*']

				minimum = 0.7
			}

			rule {
				element = 'PACKAGE'
				includes = ['com.radiotelescope.contract.*',
							'com.radiotelescope.repository.*']

				limit {
					counter = 'CLASS'
					value = 'COVEREDRATIO'
					minimum = 0.8
				}
			}
		}
	}
	dependsOn test
}

task verify(type: GradleBuild) {
	group 'Verification'
	description 'Verify the build by building, testing, and validation coverage ratio (80 or above).'
	tasks = ['build', 'test', 'jacocoTestCoverageVerification']
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')

	compile('com.fasterxml.jackson.module:jackson-module-kotlin')

	compile group: 'com.google.guava', name: 'guava', version: '19.0'
	compile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'

	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
	compile("org.jetbrains.kotlin:kotlin-reflect")

	compile group: 'com.amazonaws', name: 'aws-java-sdk-ses'
	compile group: 'com.amazonaws', name: 'aws-java-sdk-s3'

	compile("com.auth0:java-jwt:3.4.0")


	compile("org.liquibase:liquibase-core")

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	testCompile("com.h2database:h2")

	runtime('mysql:mysql-connector-java')
	runtime group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
}

//test{
//	minHeapSize = "128m"
//	maxHeapSize = "2048m"
//}